10 RESTORE 40
20 FOR X=1 TO 5
30 READ TYPE$(X)
40 DATA DIS/FIX,DIS/VAR,INT/FIX,INT/VAR,PROGRAM
50 NEXT X
60 S$="          "
70 OPEN #1:"DSK1.",INPUT ,RELATIVE,INTERNAL
80 INPUT #1:A$,G,J,K
90 PRINT "DISKNAME:     ";A$
100 PRINT "FREE SECTORS: ";STR$(K)
110 PRINT "USED SECTORS: ";STR$(J-K)
120 PRINT
130 PRINT "FILENAME   DATATYPE   SECS"
140 PRINT "---------------------------"
150 FOR L=1 TO 128
160 CALL KEY(O,K,S)
170 IF S=0 THEN 190
180 IF K=67 THEN 330
190 INPUT #1:A$,T,SCT,B
200 N=LEN(A$)
210 IF B=0 THEN 240
220 B$=STR$(B)
230 GOTO 250
240 B$=""
250 N2=LEN(B$)
260 IF N=0 THEN 330
270 D$=A$&SEG$(S$,1,11-N)&TYPE$(T)&" "&B$&SEG$(S$,1,3-N2)&STR$(SCT)
280 N=LEN(D$)
290 IF T>0 THEN 310
300 D$=D$&SEG$(S$,1,30-N)&"P"
310 PRINT D$
320 NEXT L
330 CLOSE #1
340 PRINT "R)eturn A)gain Q)uit"
350 CALL KEY(0,K,S)
360 IF S=0 THEN 350
370 IF K=65 THEN 10
380 IF K=81 THEN 400
390 GOTO 70
400 END
